apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

ext {

    releaseVersion = '0.8.0'
    snapshotVersion = "$releaseVersion-snapshot-1"

    siteUrl = 'https://github.com/wenhuiyao/CoroutinesAdapter' // Homepage URL of the library
    gitUrl = 'https://github.com/wenhuiyao/CoroutinesAdapter.git' // Git repository URL

    publishGroupId = "com.wenhui"  // Maven Group ID for the artifact

    developerId = 'wyao'
    developerName = "Wenhui Yao"
    developerEmail = 'wenhuiyao02@gmail.com'

    snapshotRepo = "snapshot"
    publishRepo = "CoroutinesAdapter"

    // Use "gradlew bintrayUpload -Ppublish" to publish the library
    if (project.hasProperty('publish')) {
        libraryVersion = releaseVersion
        pkgRepo = publishRepo
    } else {
        libraryVersion = snapshotVersion
        pkgRepo = snapshotRepo
    }

    pkgName = "com.wenhui:coroutines-adapter"
    pkgDescription = 'Adapt Kotlin coroutines to android use, can be use in Kotlin as well'
}


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 24
        versionName libraryVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.16'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:2.8.0'
    testCompile "org.robolectric:robolectric:3.3.2"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}


apply from: 'uploadArtifact.gradle'
